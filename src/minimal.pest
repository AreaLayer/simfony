WHITESPACE = _{ " " | "\t" | "\n" | "\r" }
COMMENT = _{ ("/*" ~ (!"*/" ~ ANY)* ~ "*/") | ("//" ~ (!"\n" ~ ANY)*) }

program = {
    SOI ~
    (statement ~ ";")* ~
    EOI
}
statement = { assignment | type_alias | expression }

identifier        = @{ !(reserved ~ !ASCII_ALPHANUMERIC) ~ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
jet               = @{ "jet_" ~ (ASCII_ALPHANUMERIC | "_")+ }
witness_name      = @{ (ASCII_ALPHANUMERIC | "_")+ }
builtin_type      = @{ "Either" | "Option" | "bool" | unsigned_type | "List" }
reserved          = @{ jet | builtin_type }

variable_pattern  =  { identifier }
ignore_pattern    = @{ "_" }
tuple_pattern     =  { "(" ~ ((pattern ~ ",")+ ~ pattern?)? ~ ")" }
array_pattern     =  { "[" ~ (pattern ~ ("," ~ pattern)* ~ ","?)? ~ "]" }
pattern           =  { ignore_pattern | tuple_pattern | array_pattern | variable_pattern }
assignment        =  { "let" ~ pattern ~ ":" ~ ty ~ "=" ~ expression }

left_pattern      =  { "Left(" ~ identifier ~ ")" }
right_pattern     =  { "Right(" ~ identifier ~ ")" }
none_pattern      = @{ "None" }
some_pattern      =  { "Some(" ~ identifier ~ ")" }
false_pattern     = @{ "false" }
true_pattern      = @{ "true" }
match_pattern     =  { left_pattern | right_pattern | none_pattern | some_pattern | false_pattern | true_pattern }

sum_type          =  { "Either<" ~ ty ~ "," ~ ty ~ ">" }
option_type       =  { "Option<" ~ ty ~ ">" }
boolean_type      = @{ "bool" }
unsigned_type     = @{ "u128" | "u256" | "u16" | "u32" | "u64" | "u1" | "u2" | "u4" | "u8" }
tuple_type        =  { "(" ~ ((ty ~ ",")+ ~ ty?)? ~ ")" }
array_size        = @{ ASCII_DIGIT+ }
array_type        =  { "[" ~ ty ~ ";" ~ array_size ~ "]" }
list_bound        = @{ ASCII_DIGIT+ }
list_type         =  { "List<" ~ ty ~ "," ~ list_bound ~ ">" }
ty                =  { sum_type | option_type | boolean_type | identifier | unsigned_type | tuple_type | array_type | list_type }
type_alias        =  { "type" ~ identifier ~ "=" ~ ty }

expression        =  { block_expression | single_expression }
block_expression  =  { "{" ~ (statement ~ ";")* ~ expression ~ "}" }
left_expr         =  { "Left(" ~ expression ~ ")" }
right_expr        =  { "Right(" ~ expression ~ ")" }
none_expr         = @{ "None" }
some_expr         =  { "Some(" ~ expression ~ ")" }
false_expr        = @{ "false" }
true_expr         = @{ "true" }
call_name         = ${ jet | "unwrap_left" | "unwrap_right" | "unwrap" }
call_args         =  { "(" ~ (expression ~ ("," ~ expression)*)? ~ ")" }
call_expr         =  { call_name ~ call_args }
unsigned_integer  = @{ (ASCII_DIGIT | "_")+ }
bit_string        = @{ "0b" ~ (ASCII_BIN_DIGIT | "_")+ }
byte_string       = @{ "0x" ~ (ASCII_HEX_DIGIT | "_")+ }
witness_expr      =  { "witness(\"" ~ witness_name ~ "\")" }
variable_expr     =  { identifier }
match_arm         =  { match_pattern ~ "=>" ~ (single_expression ~ "," | block_expression ~ ","?) }
match_expr        =  { "match" ~ expression ~ "{" ~ match_arm ~ match_arm ~ "}" }
tuple_expr        =  { "(" ~ ((expression ~ ",")+ ~ expression?)? ~ ")" }
array_expr        =  { "[" ~ (expression ~ ("," ~ expression)* ~ ","?)? ~ "]" }
list_expr         =  { "list![" ~ (expression ~ ("," ~ expression)* ~ ","?)? ~ "]" }
single_expression =  { left_expr | right_expr | none_expr | some_expr | false_expr | true_expr | call_expr | match_expr | tuple_expr | array_expr | list_expr | bit_string | byte_string | unsigned_integer | witness_expr | variable_expr | "(" ~ expression ~ ")" }
